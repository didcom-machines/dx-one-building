#@TYPE: Machine
#@NAME: DX-ONE DART-MX95
#@SOC: i.MX95
#@DESCRIPTION: DX-ONE DART-MX95 configuration
#@MAINTAINER: Manuel Monge <manuel.monge@didcom.com.mx>

# Override machine name
MACHINEOVERRIDES =. "mx95:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc

require include/variscite.inc
require include/variscite-tpm.inc

# BSP default providers
PREFERRED_PROVIDER_virtual/kernel:dx-one-dart-mx95 ?= "linux-variscite"
PREFERRED_PROVIDER_u-boot:dx-one-dart-mx95 ?= "u-boot-variscite"
PREFERRED_PROVIDER_u-boot-fw-utils:dx-one-dart-mx95 ?= "u-boot-fw-utils"
PREFERRED_PROVIDER_virtual/bootloader:dx-one-dart-mx95 ?= "u-boot-variscite"

MACHINE_FEATURES += "nxpiw612-sdio"

KERNEL_DEVICETREE = " \
    freescale/imx95-var-dart-dt8mcustomboard.dtb \
    freescale/imx95-var-dart-dt8mcustomboard-m7.dtb \
    freescale/imx95-var-dart-sonata.dtb \
    freescale/imx95-var-dart-sonata-m7.dtb \
"
UBOOT_DTB_NAME ?= "imx95-var-dart-dt8mcustomboard.dtb"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""

SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx95_var_dart_defconfig,sdcard"

# Set ATF platform name
ATF_PLATFORM = "imx95"
OEI_CONFIGS = "ddr tcm"
OEI_CORE   = "m33"
OEI_SOC    = "mx95"
OEI_BOARD  = "mx95-var-dart"
DDR_TYPE   = "lpddr5"

SYSTEM_MANAGER_CONFIG = "mx95evk"
IMXBOOT_TARGETS = " \
    ${@bb.utils.contains('SYSTEM_MANAGER_CONFIG', 'dart-mx95-m7', 'flash_lpboot_sm_a55', 'flash_a55', d)} \
"

IMX_BOOT_SOC_TARGET = "iMX95"
IMX_BOOT_SEEK = "32"

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

DDR_FIRMWARE_NAME = " \
    lpddr5_dmem_v202409.bin \
    lpddr5_dmem_qb_v202409.bin \
    lpddr5_imem_v202409.bin \
    lpddr5_imem_qb_v202409.bin \
"

MACHINE_EXTRA_RRECOMMENDS:remove = " imx-m7-demos"
BBMASK += "meta-imx/meta-imx-bsp/recipes-bsp/imx-mkimage/imx-boot_1.0.bbappend"
